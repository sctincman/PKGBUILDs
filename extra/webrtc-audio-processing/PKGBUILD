# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 build
#  - disable neon

pkgname=webrtc-audio-processing
pkgver=0.3
pkgrel=1
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC"
url="http://freedesktop.org/software/pulseaudio/webrtc-audio-processing"
arch=(i686 x86_64)
license=(custom)
depends=(gcc-libs)
source=("$url/$pkgname-$pkgver.tar.xz"
        'webrtc-fix-typedefs-on-other-arches.patch')
sha256sums=('756e291d4f557d88cd50c4fe3b8454ec238362d22cedb3e6173240d90f0a80fa'
            '98ba3bcf8fc11c9c76eb707ee81a3ab668f10ad8cc4915deb842a3032557d15f')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../webrtc-fix-typedefs-on-other-arches.patch
}

build() {
  cd $pkgname-$pkgver
  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"
  ./configure --prefix=/usr --disable-static $CONFIG
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
