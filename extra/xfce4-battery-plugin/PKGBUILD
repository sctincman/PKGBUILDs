# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=xfce4-battery-plugin
pkgver=1.0.5
pkgrel=5
pkgdesc="A battery monitor plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-battery-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        fix-unistd-include.patch)
sha256sums=('f659b1af40ab72c93448affaa693ab551827a5600ce9b97a799b7c2419bdeb11'
            '14428980d8ad46a28d90f587c4d8c6fe8fa8c2133eb962238e83186a88aa40c9')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../fix-unistd-include.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
