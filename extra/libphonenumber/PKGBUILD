# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make -j1

pkgname=libphonenumber
pkgver=7.5.1
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
arch=(i686 x86_64)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest)
url="https://github.com/googlei18n/libphonenumber"
license=("Apache")
source=("$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('42bb57b8c582920fc2d96ed1db07bc4792ec0731bc0f1f24dbc1e177f99bb77d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgname-$pkgver/cpp
  make -j1
}

check() {
  cd build
  make tests
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
