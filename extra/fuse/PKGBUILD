# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=fuse
pkgver=2.9.7
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
depends=('glibc')
makedepends=('pkg-config')
backup=(etc/fuse.conf)
_tag=fuse-$pkgver
source=(https://github.com/libfuse/libfuse/archive/$_tag.zip
	'fuse.conf'
	'aarch64.patch')
sha1sums=('805be38cbfb29d05036b03f2560b193010c38720'
          '3b42e37a741d4651099225987dc40e7f02a716ad'
          '0065fcb1b576ed21560c715158a88dc5e71f6611')

prepare() {
  cd libfuse-$_tag

  patch -p1 -i ../aarch64.patch
}

build() {
  cd libfuse-$_tag

  ./makeconf.sh
  export MOUNT_FUSE_PATH=/usr/bin
  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd libfuse-$_tag

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -rf ${pkgdir}/dev
}
