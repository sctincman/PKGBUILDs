# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend

pkgname=orc
pkgver=0.4.25
pkgrel=2
pkgdesc="The Oil Runtime Compiler"
arch=('i686' 'x86_64')
license=('custom')
url="https://gstreamer.freedesktop.org/projects/orc.html"
depends=('glibc')
source=(http://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz
        fix-tests.patch)
md5sums=('8582a28b15f53110c88d8043d9f55bcf'
         '0443f38fa7518a5d7de6c055a98365b7')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-tests.patch
}

build () {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
