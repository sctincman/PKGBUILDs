# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -msse4.2 compile flag

pkgname=libtorrent-rasterbar
pkgver=1.1
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${pkgver//./_}/$pkgname-$pkgver.tar.gz)
sha1sums=('ac6e871d3b71a56e849ab1fc6369165a80acfd32')

prepare() {
  # https://github.com/qbittorrent/qBittorrent/issues/5265#issuecomment-220007436
  export CXXFLAGS="$CXXFLAGS -std=c++11"

  # remove sse
  sed -i 's/-msse4.2//' libtorrent-libtorrent-${pkgver//./_}/configure.ac
}

build() {
  cd libtorrent-libtorrent-${pkgver//./_}
  ./autotool.sh
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --with-libgeoip=system \
    --with-libiconv
  make
}

package() {
  cd libtorrent-libtorrent-${pkgver//./_}
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
