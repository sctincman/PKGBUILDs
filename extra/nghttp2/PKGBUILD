# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix build on v5

pkgname=nghttp2
pkgver=1.12.0
pkgrel=1
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(i686 x86_64)
url='https://nghttp2.org/'
license=(MIT)
depends=(openssl libev zlib libxml2 jansson jemalloc)
source=(nghttp2-$pkgver.zip::https://github.com/tatsuhiro-t/nghttp2/archive/v$pkgver.zip
        https://github.com/nghttp2/nghttp2/commit/5d3535126ed9ed21c935f8006cd49c6052a6110e.patch)
backup=(
  etc/nghttpx/nghttpx.conf
  etc/logrotate.d/nghttpx
)
sha256sums=('9726cb05e17346ff60991322193bb8cf6aba22f5492217500082c5b1a62f4283'
            '5eb8384a73f4d5ea7b9cc227894fb56a2e8e22fdb45b3cb839a5762143764bac')

prepare() {
  cd nghttp2-$pkgver

  patch -p1 -i ../5d3535126ed9ed21c935f8006cd49c6052a6110e.patch
}

build() {
  cd nghttp2-$pkgver

  autoreconf -i
  ./configure \
    --prefix=/usr \
    --with-spdylay=no \
    --disable-examples \
    --disable-python-bindings
  make
}

check() {
  cd nghttp2-$pkgver
  make check
}

package() {
  cd nghttp2-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 contrib/nghttpx.service "$pkgdir/usr/lib/systemd/system/nghttpx.service"
  install -Dm644 contrib/nghttpx-logrotate "$pkgdir/etc/logrotate.d/nghttpx"
  install -Dm644 nghttpx.conf.sample "$pkgdir/etc/nghttpx/nghttpx.conf"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/nghttp2/COPYING"
}
