# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#        Romain Reignier <rom.reignier AT gmail.com>
# - add specfic CXXFLAGS for armv7h and aarch64

buildarch=12

pkgname=intel-tbb
pkgver=4.4_20160526
_tag=tbb${pkgver/\./}oss
_file=${_tag}_src_0.tgz
pkgrel=2
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(http://threadingbuildingblocks.org/sites/default/files/software_releases/source/$_file)
sha1sums=('b08adf14c65511f1e9813a3e289d72f9a6cd3006')

build() {
  cd $_tag
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  [[ $CARCH == "armv7h" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"
  make
}

package() {
  cd $_tag
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
