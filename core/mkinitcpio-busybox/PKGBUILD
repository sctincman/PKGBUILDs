# $Id: PKGBUILD 141111 2011-10-24 09:59:20Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed -mtune, changed -march to v8/v7/v6/v5 compat

pkgname=mkinitcpio-busybox
pkgver=1.24.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=("http://busybox.net/downloads/busybox-$pkgver.tar.bz2"
        'glibc-2.16.patch'
        'config')
sha256sums=('e71ef53ec656f31c42633918d301405d40dea1d97eca12f272217ae4a971c855'
            'fb5b6e2a0de4db5401322e5c2474ad8ce6a58615dad45b7109cfe045baf2c88d'
            'fd4e5571d9bdde368e01d601a9e05df05001827bffb2f574187b4f47db4cec55')

prepare() {
  cd "busybox-$pkgver"

  if [ ${CARCH} = "aarch64" ]; then
    ARMCARCH="armv8-a"
  elif [ ${CARCH} = "armv7h" ]; then
    ARMCARCH="armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16"
  elif [ ${CARCH} = "armv6h" ]; then
    ARMCARCH="armv6 -mfloat-abi=hard -mfpu=vfp"
  else
    ARMCARCH="armv5te"
  fi

  local safeflags="-march=${ARMCARCH} -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="'"$safeflags"'"|' "$srcdir/config" >.config

  patch -Np1 <"$srcdir/glibc-2.16.patch"
}

build() {
  make -C "busybox-$pkgver"
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}

# vim:set ts=2 sw=2 et:
