# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NO_X86=1

pkgname=afl
pkgver=2.12b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url="http://lcamtuf.coredump.cx/afl/"
arch=('i686' 'x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
  'llvm: experimental clang-fast instrumentation support'
  'gnuplot: graph plotting support'
)
makedepends=('clang' 'llvm' 'gcc' 'wget' 'python')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('76e5018cc7ece1e9d164e91ec3552925454b85bf9d981fc2eb6a718fdb06741796b323605ddba93d01df0d9512309ba89554735b7b08c5bf8ad69257b3665e64')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1 DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm "${pkgdir}/usr/share/doc/${pkgname}/README.llvm"
}

# vim: ts=2 sw=2 et:
