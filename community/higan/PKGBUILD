# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=higan
pkgver=101
pkgrel=1
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('alsa-lib' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'gtksourceview2' 'libao' 'libgl' 'libpulse' 'libx11' 'libxext' 'libxv'
         'openal' 'pango' 'sdl'
         'libudev.so')
makedepends=('mesa')
conflicts=('higan-gtk' 'higan-qt')
replaces=('higan-gtk' 'higan-qt')
install='higan.install'
source=("http://download.byuu.org/higan_v${pkgver}-source.7z"
        'higan-flags.patch'
        'sjlj-declare-xopen-source.patch')
sha256sums=('c82f2128eb8c6f4728e4c1e2579666f723b02b0666a786f0c19c8d05ae1b7913'
            '6ee3489a3cf3e8d09beaa5c1f809824593aa08b61dc9a8c9ddc12a09c89ae6cb'
            '2c5af5006f0438a7d7bfe62aa0c540b9e6ee526d45dfcafd65bb8598d50d83d7')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-flags.patch
  patch -Np1 -i ../sjlj-declare-xopen-source.patch
}

build() {
  cd higan_v${pkgver}-source

  make -C icarus
  make -C higan
}

package() {
  cd higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,higan,pixmaps}}
  install -m 755 icarus/out/icarus "${pkgdir}"/usr/bin/
  install -m 755 higan/out/higan "${pkgdir}"/usr/bin/
  install -m 644 higan/data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 higan/data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' higan/systems/* higan/data/cheats.bml "${pkgdir}"/usr/share/higan/
}

# vim: ts=2 sw=2 et:
