# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix for unsigned char on ARM

pkgname=rawtherapee
pkgver=4.2+448.g26d182d
_pkgver=26d182dabc6f133c27bffa3aac3f91b7ae55d578
pkgrel=3
epoch=1
pkgdesc="Raw photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL3')
depends=('fftw' 'gtkmm' 'libcanberra' 'libiptcdata' 'lcms2' 'hicolor-icon-theme')
optdepends=('gtk-engines: workaround theme bug under GNOME')
makedepends=('cmake' 'git')
#source=("http://rawtherapee.com/shared/source/rawtherapee-${pkgver}.tar.xz")
#md5sums=('e6510ed56fdc35aa712b4c0f54c52ac0')
source=("rawtherapee::git+https://github.com/Beep6581/RawTherapee.git#commit=$_pkgver"
        rawtherapee-gcc6.patch::"https://github.com/Beep6581/RawTherapee/commit/5cd202dd.patch"
        '0001-char-fix-for-ARM.patch')
md5sums=('SKIP'
         '398a38f531f7c8c37b4789f9560914e2'
         '585a1fc6a1e4e1ce5c479279eba44eb0')

prepare() {
  cd $pkgname
  # Fix build with GCC 6 
  patch -p1 -i ../rawtherapee-gcc6.patch

  git apply ../0001-char-fix-for-ARM.patch
}

build() {
  mkdir -p "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/$pkgname" \
    -DCMAKE_CXX_FLAGS="-std=c++11" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}


# vim:set ts=2 sw=2 et:
