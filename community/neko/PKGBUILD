# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>
# Contributor: Christian Hesse <arch@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove unsupported gcc flag on !aarch64

pkgname=neko
pkgver=2.1.0
pkgrel=1
pkgdesc='High-level dynamically typed programming language'
url='http://nekovm.org/'
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmariadbclient' 'sqlite' 'apache' 'mbedtls')
makedepends=('pkgconfig' 'apr' 'git' 'ninja' 'mbedtls' 'cmake')
options=('!strip')
source=("git://github.com/HaxeFoundation/neko#tag=v${pkgver//./-}")
sha256sums=('SKIP')

prepare() {
  if [[ $CARCH != "aarch64" ]]; then sed -i '/mincoming-stack-boundary/d' $pkgname/CMakeLists.txt; fi
}

build() {
  mkdir -p build
  cd build

  cmake "../$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: nekovm.org/download
# vim:set ts=2 sw=2 et:
