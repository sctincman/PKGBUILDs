# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20160528
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch')
md5sums=('aa2ec2fd3a377fb5a1e7b0d2e491f99b'
         '2dd231cc55cb66dabcd0e9770ff727e9'
         'ed6f41393b2441f7e2185331be256921'
         'da8409a3b73b345e2d888fe233d19cdb'
         '093c6c2801a1d7c79a7384d64c520837'
         '3d69e998b3f9e10a75b9806ef9bcaa83'
         '52b92a26640a2539cf077eba0a2492ce')

prepare() {
	cd "${_pkgname}-${pkgver}"
	patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
	patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i ../0003-makechrootpkg-distcc.patch
	patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
	patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i ../0006-archbuild-no-setarch.patch
}

build() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
