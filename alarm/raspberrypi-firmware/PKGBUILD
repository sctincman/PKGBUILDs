buildarch=20

pkgbase=raspberrypi-firmware
pkgname=('raspberrypi-firmware'
         'raspberrypi-firmware-bootloader'
         'raspberrypi-firmware-bootloader-x'
         'raspberrypi-firmware-tools'
         'raspberrypi-firmware-examples')
pkgver=20160708
pkgrel=1
_commit=218718e01aeb3de618ab2c3661d86645de6f21d9
pkgdesc="Firmware files for Raspberry Pi"
arch=('any')
url="https://github.com/raspberrypi/firmware"
makedepends=('git')
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz"
        '00-raspberrypi-firmware.conf')
md5sums=('2a28860ad6c5e6f48c90a42e0acfdec2'
         '72e0d5818fc513ece1b964f25f7e7882')

package_raspberrypi-firmware() {
  depends=('raspberrypi-firmware-bootloader'
           'raspberrypi-firmware-bootloader-x'
           'raspberrypi-firmware-tools'
           'raspberrypi-firmware-examples')
}

package_raspberrypi-firmware-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-firmware-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}

package_raspberrypi-firmware-tools() {
  pkgdesc="Firmware tools and header files for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/{bin,include,lib,sbin,LICENCE} "${pkgdir}"/opt/vc

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
  cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
}

package_raspberrypi-firmware-examples() {
  pkgdesc="Firmware example sources for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/src "${pkgdir}"/opt/vc
}
