# U-Boot: Raspberry Pi
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-raspberrypi
pkgver=2016.03rc2
pkgrel=1
_pkgver=${pkgver/rc/-rc}
pkgdesc="U-Boot for Raspberry Pi"
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'dtc' 'git')
conflicts=('linux-raspberrypi')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${_pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'boot.txt'
        'mkscr')
md5sums=('0f0068341f8124ffab0f966808ba6236'
         '2fc5fdd1fb48545336a2f534772d7477'
         '3631f52fb2a0e940bbcc04ea80701d9a'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${_pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_pkgver}

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  make distclean 
  make rpi_2_config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_pkgver}

  mkdir -p "${pkgdir}"/boot

  cp u-boot.bin ${pkgdir}/boot/kernel7.img

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}"/boot/boot.scr
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
