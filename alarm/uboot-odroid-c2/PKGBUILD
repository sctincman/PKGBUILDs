# U-Boot: ODROID-C2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c2
pkgver=2015.01
pkgrel=1
pkgdesc="U-Boot for ODROID-C2"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=5785ef87af8998195c94af38a6b6d0b6ef327afa
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-remove-cross-compiling.patch'
        '0002-sd_fusing-tweaks.patch'
        '0003-Add-linux-compiler-gcc5.h-to-fix-builds-with-gcc5.patch'
        '0004-arch-linux-arm-modifications.patch')
md5sums=('46c3c2c4e28e18c04e81fc878868f5ca'
         '04b71c2e7ec5734f9646cfed8d26f7ae'
         '2ade162338fe32a4a156eede084222c0'
         '296596234d264ae7d5278e2c4068144f'
         '75d6a8bf332b61b8499bb49e99918078'
         'e15e3965405942b388f5aa51b4880cce')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-remove-cross-compiling.patch
  git apply ../0002-sd_fusing-tweaks.patch
  git apply ../0003-Add-linux-compiler-gcc5.h-to-fix-builds-with-gcc5.patch
  git apply ../0004-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean
  make odroidc2_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp bl1.bin.hardkernel sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
}
