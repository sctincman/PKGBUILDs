# U-Boot: ODROID-C2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c2
pkgver=2015.01
pkgrel=8
pkgdesc="U-Boot for ODROID-C2"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=3edccd18784aef602a50040b24acdf3adbb7781a
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.patch'
        '0002-arch-linux-arm-modifications.patch')
md5sums=('e112b3e9c77358bda6b14ce5347e995a'
         'a0ba81b25ad88f1f436b9f67bd815f35'
         '316b458415515edfc18abd54a52d43d4'
         'de51b9b3345954238c455214c5947ea3')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-sd_fusing-tweaks.patch
  git apply ../0002-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean
  make odroidc2_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp bl1.bin.hardkernel sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
}
